buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.8"
    id 'net.researchgate.release' version '2.4.0'
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'

sourceCompatibility = 1.8

idea {
    module {
        generatedSourceDirs += file('gen')
    }
}

sourceSets {
    main {
        java.srcDirs += 'gen'
    }
}

clean.doFirst {
    delete 'gen'
}

intellij {
    pluginName 'Non-Dairy-Soy-Plugin'

    version '2016.3.2'
    downloadSources Boolean.valueOf(true)
    updateSinceUntilBuild = false

    sandboxDirectory project.rootDir.canonicalPath + "/.sandbox"
    systemProperties = ['compiler.process.debug.port': '21232']
}

repositories {
    jcenter()
}

configurations {
    lexer
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.github.vbmacher:java-cup-runtime:11b-20160615'

    lexer files('lib/jflex-1.7.0-SNAPSHOT.jar') //'de.jflex:jflex:1.6.1'
}

release {
    newVersionCommitMessage = '[Non-Dairy-Soy-Plugin Release] - '
    preTagCommitMessage = '[Non-Dairy-Soy-Plugin Release] - pre tag commit: '
    buildTasks = ['buildPlugin']
}


task codegen(dependsOn: ['lexer']) {}
tasks.compileJava.dependsOn codegen

task lexer(type: JavaExec) {
    def lexerName = "_SoyLexer"
    def pkg = "net/venaglia/nondairy/soylang/lexer"
    def src = "$project.projectDir/src/main/grammars/${lexerName}.flex"
    def dst = "gen/$pkg"
    new File(dst).mkdirs()

    main = 'jflex.Main'
    classpath configurations.lexer
    args = ['--skel', 'src/main/grammars/idea-flex.skeleton', '-d', dst, src]

    inputs.file file(src)
    outputs.dir file("$dst/${lexerName}.java")
}
